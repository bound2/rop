group 'com.snyberichapp'
version project.property('projectVersion')

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jacksonVersion"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "$jacksonVersion"
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: "$jacksonVersion"
    compile group: 'junit', name: 'junit', version: "$junitVersion"
    compile group: 'org.testng', name: 'testng', version: "$testngVersion"
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

def pomConfig = {
    licenses {
        license {
            name 'MIT'
            url 'https://raw.githubusercontent.com/bound2/rop/master/LICENSE'
            distribution 'repo'
        }
    }
    developers {
        developer {
            name 'bound2'
            email 'snyberichapp@gmail.com'
        }
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact jar
            groupId 'com.snyberichapp.tools'
            artifactId 'response-object-proxy'
            version project.property('projectVersion')
            pom.withXml {
                def root = asNode()
                def dependenciesNode = root.appendNode('dependencies')
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'tools'
        name = 'response-object-proxy'
        userOrg = 'snyberichapp'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/bound2/rop.git'
        websiteUrl = 'http://snyberichapp.com'
        version {
            name = project.property('projectVersion')
            desc = 'Response object proxy - testing library'
            released = new Date()
        }
        publications = ['Production']
    }
}